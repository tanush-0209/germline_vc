#!/usr/bin/env nextflow

include { VALIDATE }         from './modules/validate'
include { FASTQC }           from './modules/fastqc'
include { FASTP }            from './modules/fastp'
include { BWA_MEM }          from './modules/bwa_mem'
include { MARKDUPLICATES }   from './modules/markduplicates'
include { HAPLOTYPECALLER }  from './modules/haplotypecaller'
include { JOINT_GENOTYPE }   from './modules/joint_genotype'
include { FILTER }           from './modules/filter'
include { MULTIQC }          from './modules/multiqc'
include { PUBLISH }          from './modules/publish'

sheet = file(params.samplesheet)
ch_samples = channel.fromPath(sheet)
    .splitCsv(header: true)
    .map { row ->
        def meta = [id: row.sample_id, single_end: row.library_type == 'single']
        def r1 = sheet.parent.resolve(row.r1_fastq)
        def reads = meta.single_end ? [r1] : [r1, sheet.parent.resolve(row.r2_fastq)]
        [meta, reads]
    }

ref       = file(params.ref)
ref_index = files("${params.ref}.*")
ref_dict  = file("${ref.parent}/${ref.baseName}.dict")

workflow {
    main:

    VALIDATE(sheet, ch_samples.map { _meta, reads -> reads }.collect(),
             ref, ref_index, ref_dict)

    ch_checked = ch_samples
        .combine(VALIDATE.out.sheet)
        .map { meta, reads, _validated -> [meta, reads] }

    FASTQC(ch_checked)
    FASTP(ch_checked)
    BWA_MEM(FASTP.out.reads, ref, ref_index)
    MARKDUPLICATES(BWA_MEM.out)
    HAPLOTYPECALLER(MARKDUPLICATES.out.bam, ref, ref_index, ref_dict)

    JOINT_GENOTYPE(
        HAPLOTYPECALLER.out.gvcf.collect(),
        HAPLOTYPECALLER.out.tbi.collect(),
        ref, ref_index, ref_dict
    )

    FILTER(JOINT_GENOTYPE.out.vcf, JOINT_GENOTYPE.out.tbi, ref, ref_index, ref_dict)

    ch_reports = FASTQC.out.mix(FASTP.out.json, MARKDUPLICATES.out.flagstat)
        .collect()
    MULTIQC(ch_reports)

    PUBLISH(VALIDATE.out.sheet, FILTER.out.vcf.mix(FILTER.out.table, MULTIQC.out).collect())

    publish:
    vcf      = FILTER.out.vcf
    variants = FILTER.out.table
    multiqc  = MULTIQC.out
    manifest = PUBLISH.out.manifest
    samples  = PUBLISH.out.samples
}

output {
    vcf {
        path '.'
    }
    variants {
        path '.'
    }
    multiqc {
        path '.'
    }
    manifest {
        path '.'
    }
    samples {
        path '.'
    }
}
